ZVSE
ERMS_ScriptName=script92 Bad Luck / HoMM4-Mageguildes
ERMS_ScriptAuthor=Thomas Franz (alias samot)
ERMS_ScriptVersion=1.0.0
ERMS_ScriptDate=30.5(May).2006
ERMS_ScriptERMVersion=2.70
ERMS_ScriptLanguage=English (United States) or English (native)
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945

***************** Bad luck script *****************
** Last update: 30th May 2006 (corrected usage)

** Written by Thomas Franz (alias samot)
** if the stack in combat has negativ luck it may happens on of the following 4 things
** with the same chances as good luck (4,2% per negativ luck point):
** 1. the stack makes only its minimum damage (as cursed)
** 2. the stack makes only 75 % of its origin damage
** 3. all units in the stack looses 10% of their total health
** 4. the stack has only 50 % of its origin defense against the retailation OR
**    if the stack shoots they loose 3 shoots

** Bad luck will never occure for the retailation of a stack (only if it attacks by itself)

** Furthermore this script modifies the Hourglass of Evil Hour so that they give
** yourself neutral luck but always -1 to the enemy.
** Antichristi / Hellbarons give -2 luck for their enemys.

** It will also set the new empty fuelhorn artifact (170) so that it will decrease
** the luck of your enemy by 1 but doesn't touch your own luck

** Wog-Option 92
** Functions used: FU1620
** variables used: v917-v919; v944-v945
** String vars: z177 / z178 are EXCLUSIVE used for name / description of fuelhorn artifact and description for
**              the hourglass
** Re-Use of z269 which is used in the script06 (Hourglass of Asmodeus) for its description
** (modify this description or set it if script06 is not active); EXCLUSIVE



*************** start bad luck script ***************


!#UN:P92/?v1;
!#VRz177&v1=1:Sz192000;
!#VRz178&v1=1:Sz192001;
!#UN&v1=1:A170/9/177; // set name (fuelhorn)
!#UN&v1=1:A170/10/178; // set description
!#UN&v1=1:A46/1/?v1; // get value of Clover of Fortune
!#UN&v1=1:A170/1/v1; // and set it to the horn
!#UN&v1=1:P106/?v2; // ceck if Hourglass of Asmodeus is activated
!#VRz1&v1=1:S^^; // clear z1
!#VRz1&v2=1:Sz192003;
!#VRz269&v1=1:Sz192004;
!#UN&v1=1:A85/10/269; // set description of hourglass


!?BG0;
** Vars used:
** y-1   = Stack number
** y-2   = current action of the stack
** y-3   = current hero
** y-4   = opposite hero
** y-5   = current site (0 = attacker; 1 = defender)
** y-6   = opposite site
** y-7   = luck of current hero / army
** y-8   = the opposites hero has the Hourglass of the Evil Hour equipped (give -1 luck to the opposite)
** y-9   = fortune / misfortune is casted to current stack
** y-10  = level of heroes fire / airmagic (used to get luck bonus / malus)
** y-11  = Type of battle ground
** y-12  = Random number for bad-luck-chance and effect
** y-97  - y-99 = temp Vars for different usage
** v917 = effect of bad luck
** v918 = stack effected by bad luck
** v919, v944 - v945 = its original MinDam, MaxDam and Defense

!!UN:P92/?y-1; !!FU&y-1=0:E; // check if script is enabled; exit if not

!!VRv917:S0;
!!BG:N?y-1; // get current stack number
!!BG:A?y-2; // get current action (6=walk and attack, 7 = shoot)
!!FU|y-2<6/y-2>7/y-1<0/y-1>41:E; // exit Function if no attack-action or no valid stack

!!BMy-1:T=149; // check, if stack = Arrow Towers
!!FU&1:E; // if yes: exit

!!BG:Q?y-5; // get the current attacking site
!!VRy-6:S0; !!VRy-6&y-5=0:S1; // set y-6 to the opposite of y-5
!!BA:Hy-5/?y-3; !!BA:Hy-6/?y-4; // current / opposite hero

** calculating luck for current stack
!!VRy-7:S0;
!!FU2333&y-3>-1:Py-3/?y-7; // get the luck of current hero (if there is one), uses FU2333 from script64

** correction for devils in the opposite army
!!VRr:S0; // initialize result-var
!!VRy-99:Sy-6 +1 *20; // get max. stack-number of opposite; y-99 will be 20 if attacker is active and 40 if defender is active
!!VRy-99&y-99=40:+1; // correct y-98 from 40 to 41
!!VRy-98:Sy-99 -20; // min stack number (0 or 21) of the opposite
!!DO1620/y-98/y-99/1:P; // check for devils / archdevils / antichrist in the opposits army
!!VRy-7:-r; // decrease luck if there are devils

** correction for fortune spell
!!VRy-10:S0;
!!BMy-1:G51/?y-9/?y-10; // check for active fortune spell
!!VRy-7&y-9>0:+y-10; // y-10 is the power of the spell

** correction for misfortune spell
!!VRy-10:S0;
!!BMy-1:G52/?y-9/?y-10; // check for active misfortune spell
!!VRy-7&y-9>0:-y-10; // y-10 is the power of the spell

** correction for battle ground
!!BA:P?y-97/?y-98/?y-99; // get position of battle
!!TRy-97/y-98/y-99:G?y-11; // get the battle ground
!!BMy-1:T?y-96; // get actual monster ty-pe
!!IF:V1/0; // disable Flag 1
!!IF&y-96>83/y-96<150:V1/1; // enable if stronghold, fortress, conflux or neutral creature
!!IF&y-96>155/y-96<174:V1/1;
!!IF|y-96=181/y-96=182/y-96=180/y-96=189/y-96=190/y-96=191/y-96=193/y-96=194/y-96=195:V1/1;
!!VRy-7&y-11=222/1:+2; // add 2 to luck if battle ground is coverfield and it is an neutral unit

** correction for Horn of bad luck (blank artifact 170)
!!VRy-8:S0;
!!HEy-4&y-4>-1:A2/170/d/?y-8; // check if the opposit hero equips the Horn of Bad Luck
!!VRy-7&y-8>0:-1; // if yes decrease luck by 1

** correction for Hourglass of the Evil Hour
!!HEy-3&y-3>-1:A2/85/d/?y-8; // check if the current hero equips the Hourglass of the Evil Hour
!!FU&y-8>0:E; // if yes exit (it will negate all luck (positive and negative) for its army)
!!VRy-8:S0;
!!HEy-4&y-4>-1:A2/85/d/?y-8; // check if the opposite hero equips the Hourglass of the Evil Hour
!!VRy-7&y-8>0:S-1; // if yes set luck to -1


** effects if luck is negative
!!VRy-12:S0 R1000; // random number between 0 and 1000
!!VRy-99&y-7<0:Sy-7 * -42; // set y-99 to the chance of bad luck (4,2% per negative luck level)
!!IF:V1/0; // set flag 1 to 0
!!IF&y-7<0/y-12<y-99:V1/1; // set flag 1, if the odds are bad for you
!!FU&-1:E; // exit if flag 1 is not set (no bad luck because luck is positiv or neutral or the odds are good)

** if misfortune appears
!!BMy-1:V48;  // show animation for misfortune
!!VRy-12:S0 R3; // random number between 0 and 3
!!VRv918:Sy-1; // save stack to v918
!!BMy-1:U1/?v919; // get stacks MinDam to v919
!!BMy-1:U2/?v945; // get stacks MaxDam to v945
!!BMy-1:D?v944; // get stacks Defense to v944
!!UN:N3/z-1/y-96/1; // get the name of the monsters in the stack (y-96 was set to the monster type in the battleground-section)

** effect 1: the stack loses 10 % of its max. health
!!VRv917:S0; // save the effect to v917
!!BMy-1&y-12=0:H?y-99; // get HPs
!!VRy-98&y-12=0:Sy-99 :9 *10; // get 90 %
!!VRy-98&y-98=y-99:-1; // at least one HP lost
!!VRy-98&y-98<1:S1; // at leaste one remaining HP (f.e. peasants)
!!BMy-1&y-12=0:Hy-98; // set new HP
!!VRz-2&y-12=0:Sz192005;

** effect 2: Curse - MaxDam = MinDam
!!BMy-1&y-12=1:U2/v919; // set MaxDam to MinDam (works like curse)
!!VRz-2&y-12=1:Sz192006;

** effect 3: reduces Dam to 75% of its original Dam
!!VRy-98&y-12=2:Sv919 *3 :4;  // get 75 % of MinDam
!!VRy-99&y-12=2:Sv945 *3 :4; // get 75 % of MaxDam
!!BMy-1&y-12=2:U1/y-98; !!BMy-1&y-12=2:U2/y-99; // set it to the stack
!!VRz-2&y-12=2:Sz192007;

** effect 4: reduces Defens to 50 % of the stacks origin Defens (bad if the enemy is retaliating)
!!VRy-12&y-2=7:S4; // if stack shoots set effect to the 5th because there is no retaliation against shoots
!!VRv917&y-12=3:S3; // save the effect to v917
!!VRy-99&y-12=3:Sv944 :2;
!!BMy-1&y-12=3:Dy-99; // set the reduced Defense
!!VRz-2&y-12=3:Sz192008;

** effect 5: loose 3 shoots
!!VRv917&y-12=4:S0; // save the effect to v917 (no reset neccessary in BG1-trigger)
!!BMy-1&y-12=4:U3/d-3;
!!VRz-2&y-12=4:Sz192009;

!!BU:Mz-2; // show the misfortune-message

*** end function BG0 ***


!?BG1&v917>0; // after the turn and if there was one of the bad-luck-effects 1-3 active
!!UN:P92/=1; !!FU&-1:E; // check if script is enabled; exit if not
!!BMv918:U1/v919; // restore MinDam
!!BMv918:U2/v945; // restore MaxDam
!!BMv918:Dv944;   // restore Defense
*** end function BG1 ***


!?FU1620; // check for devils / archdevils / antichrist in the opposit army
** x1 = Site of the opposit (if the current Stack is attacker (left side) it will be defender and vice versa)
** r  = returns the luck malus for devils (+1 or +2, you have to decrease the luck by r)
!!BMx16:T?y1;
!!VRr&y1>53/y1<56/r=0:S1; // set r to 1 if there are devils / archdevils
!!VRr&y1=153/r<2:S2; // set r to 2 if there are antichrists

*** end function FU1620 ***

*************** end bad luck script ***************
